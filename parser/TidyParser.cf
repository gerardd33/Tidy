-- GLOBAL -------------------------------------

token UpperCaseIdent (upper (letter | digit | '_')*) ;
token LowerCaseIdent (lower (letter | digit | '_')*) ;


-- PROGRAMS -----------------------------------

entrypoints Program ;
ProgramEntrypoint. Program ::= [ClassDecl] ;


-- COMMENTS -----------------------------------

comment "//" ;
comment "/*" "*/" ;


-- CLASS DECLARATIONS -------------------------

separator ClassIdent "," ;
CIdent. ClassIdent ::= UpperCaseIdent ;

separator nonempty ClassDecl "" ;
ClassDeclConcrete. ClassDecl ::= ClassType "class" ClassIdent Inheritance ClassBody ;
ClassDeclAbstract. ClassDecl ::= "abstract" ClassType "class" ClassIdent Inheritance ClassBody ;

SuperclassAbsent. Inheritance ::= ;
SuperclassPresent. Inheritance ::= "extends" ClassIdent ;


ClassBodyEmpty. ClassBody ::= ;
ClassBodyFilled. ClassBody ::= "{" ValuesSection VariablesSection FunctionsSection ActionsSection "}" ;


-- CLASS MODIFIERS ----------------------------

MMutable. ClassType ::= "mutable" ;
MImmutable. ClassType ::= "immutable" ;
MSingleton. ClassType ::= "singleton" ;


-- CLASS SECTION DECLARATIONS -----------------

ValuesAbsent. ValuesSection ::= ;
ValuesPresent. ValuesSection ::= "values:" ValSBody ;

VariablesAbsent. VariablesSection ::= ;
VariablesPresent. VariablesSection ::= "variables:" VarSBody ;

FunctionsAbsent. FunctionsSection ::= ;
FunctionsPresent. FunctionsSection ::= "functions:" FSBody ;

ActionsAbsent. ActionsSection ::= ;
ActionsPresent. ActionsSection ::= "actions:" ASBody ;


-- VALUES SECTION -----------------------------

ValSBody. ValSBody ::= "{" [ValueDecl] "}" ;

VIdent. ValueIdent ::= LowerCaseIdent ;

ValueTypeClass. ValueType ::=  ClassIdent ;
ValueTypeGeneric. ValueType ::= ClassIdent "[" [ClassIdent] "]" ;
ValueTypeFunction. ValueType ::= "get" MethodType ;
ValueTypeAction. ValueType ::= "do" MethodType ;

terminator ValueDecl ";" ;
PublicValueDecl. ValueDecl ::= ValueDeclProper ;
PrivateValueDecl. ValueDecl ::= "private" ValueDeclProper ;

UninitialisedValue. ValueDeclProper ::= ValueIdent ":" ValueType ;
InitialisedValue. ValueDeclProper ::= ValueIdent ":" ValueType "=" Expr ;


-- VARIABLES SECTION --------------------------

VarSBody. VarSBody ::= "{" [ValueDecl] "}" ;


-- FUNCTIONS SECTION --------------------------

FSBodyEmpty. FSBody ::= ;
FSBodyFilled. FSBody ::= "{" [FunctionDecl] "}" ;

FIdent. FunctionIdent ::= LowerCaseIdent ;
FType. MethodType ::= "(" [ValueDecl] ")" "->" ValueType;

separator FunctionDecl "" ;
OverrideFunctionDecl. FunctionDecl ::= "override" FunctionIdent ":" MethodType "=" FunctionBody ;
PublicFunctionDecl. FunctionDecl ::= FunctionIdent ":" MethodType "=" FunctionBody ;
PrivateFunctionDecl. FunctionDecl ::= "private" FunctionIdent ":" MethodType "=" FunctionBody ;

FunctionBodyOneLine. FunctionBody ::= Expr ;
FunctionBodyMultiLine. FunctionBody ::= "{" Expr "}" WithValues ;

WithValuesAbsent. WithValues ::= ;
WithValuesPresent. WithValues ::= "with" ValuesSection ;


-- ACTIONS SECTION ----------------------------

ASBodyEmpty. ASBody ::= ;
ASBodyFilled. ASBody ::= "{" [ActionDecl] "}" ;

separator ActionDecl "" ; 
OverrideActionDecl. ActionDecl ::= "override" FunctionIdent ":" MethodType "=" ActionBody ;
PublicActionDecl. ActionDecl ::= FunctionIdent ":" MethodType "=" ActionBody ;
PrivateActionDecl. ActionDecl ::= "private" FunctionIdent ":" MethodType "=" ActionBody ;

ActionBodyOneLine. ActionBody ::= Expr ;
ActionBodyMultiLine. ActionBody ::= "{" [Expr] "}" ;


-- EXPRESSIONS -------------------------------

separator Expr "" ;
coercions Expr 7 ;

ELiteral. Expr7 ::= Literal ;
ELocalValue. Expr7 ::= ValueIdent ;

EUnaryNot. Expr6 ::= "not" Expr7 ;
EUnaryMinus. Expr6 ::= "-" Expr7 ;

ELambdaFunction. Expr5 ::= LambdaFunction ;
ELambdaAction. Expr5 ::= LambdaAction ;

ELocalFunctionCall. Expr5 ::= "local" FunctionCall ;
ELocalActionCall. Expr5 ::= "local" ActionCall ;
ECtorCall. Expr5 ::= ConstructorCall ;

EGetExpr. Expr5 ::= GetExpr ;
EDoExpr. Expr5 ::= DoExpr ;

EImperativeControlFlow. Expr5 ::= ImperativeControlFlow ;
EFunctionalControlFlow. Expr5 ::= FunctionalControlFlow ;

ELocalValueDecl. Expr5 ::= LocalValueDecl ;

EMultiply. Expr3 ::= Expr3 "*" Expr4 ;
EDivide. Expr3 ::= Expr3 "/" Expr4 ;

EAdd. Expr2 ::= Expr2 "+" Expr3 ;
ESubtract. Expr2 ::= Expr2 "-" Expr3 ;
EConcatenate. Expr2 ::= Expr2 "++" Expr3 ;

ERelationalOperator. Expr1 ::= Expr1 RelationalOperator Expr2 ;

EBooleanOperator. Expr ::= Expr1 BooleanOperator Expr ;


-- LITERALS ----------------------------------

LInt. Literal ::= Integer ;
LBool. Literal ::= Boolean ;
LChar. Literal ::= Char ;
LString. Literal ::= String ;
LVoid. Literal ::= Void ;

BTrue. Boolean ::= "True" ;
BFalse. Boolean ::= "False" ;
VPass. Void ::= "Pass" ;


-- LOCAL VALUE DECLARATIONS ------------------

LocalVDecl. LocalValueDecl ::= "value" ValueDecl ";" ;


-- LAMBDAS -----------------------------------

LambdaFunctionOneLine. LambdaFunction ::= "get" "(" [ValueDecl] ")" "->" Expr ";" ;
LambdaFunctionMultiLine. LambdaFunction ::= "get" "(" [ValueDecl] ")" "->" "{" Expr "}" ;

LambdaActionOneLine. LambdaAction ::= "do" "(" [ValueDecl] ")" "->" Expr ";" ;
LambdaActionMultiLine. LambdaAction ::= "do" "(" [ValueDecl] ")" "->" "{" [Expr] "}" ;


-- FUNCTION CALLS ----------------------------

FunctionCallNoArgs. FunctionCall ::= "." FunctionIdent ;
FunctionCallWithArgs. FunctionCall ::= "." FunctionIdent "(" [FunctionArgument] ")" ;

ActionCallNoArgs. ActionCall ::= "#" FunctionIdent ;
ActionCallWithArgs. ActionCall ::= "#" FunctionIdent "(" [FunctionArgument] ")" ;

separator FunctionArgument "," ;
FunctionArg. FunctionArgument ::= Expr ;


-- CONSTRUCTOR CALLS --------------------------

CtorCallNoArgs. ConstructorCall ::= ClassIdent ;
CtorCallWithArgs. ConstructorCall ::= ClassIdent "(" [FunctionArgument] ")" ;


-- GET EXPRESSIONS ----------------------------

GetExprInstance. GetExpr ::= ValueIdent FunctionCall ;
GetExprStatic. GetExpr ::= ClassIdent FunctionCall ;
GetExprChain. GetExpr ::= GetExpr FunctionCall ;


-- DO EXPRESSIONS ----------------------------

DoExprInstance. DoExpr ::= ValueIdent ActionCall ;
DoExprStatic. DoExpr ::= ClassIdent ActionCall ;
DoExprChain. DoExpr ::= GetExpr ActionCall ;


-- IMPERATIVE CONTROL FLOW -------------------

IWhile. ImperativeControlFlow ::= "while" "(" Expr ")" "{" [Expr] "}" ;
IForeach. ImperativeControlFlow ::= "for" "(" ValueDecl "in" Expr ")" "{" [Expr] "}" ;
IIf. ImperativeControlFlow ::= "if" "(" Expr ")" "{" [Expr] "}" OptionalElseBranch ;

ElsePresent. OptionalElseBranch ::= "else" "{" [Expr] "}" ;
ElseAbsent. OptionalElseBranch ::= ;


-- FUNCTIONAL CONTROL FLOW -------------------

FIfThenElse. FunctionalControlFlow ::= "if" "(" Expr ")" ThenBranch ElseBranch ;

ThenOneLine. ThenBranch ::= "then" Expr ";" ;
ThenMultiLine. ThenBranch ::= "then" "{" Expr "}" ;
ElseOneLine. ElseBranch ::= "else" Expr ";" ;
ElseMultiLine. ElseBranch ::= "else" "{" Expr "}" ;

FMatch. FunctionalControlFlow ::= "match" Expr "{" [MatchCase] "}" ;

separator MatchCase "" ;
Case. MatchCase ::= "case" Pattern "->" Expr ;

TypePattern. Pattern ::= ClassIdent ; 


-- RELATIONAL OPERATORS ----------------------

RLess. RelationalOperator ::= "<" ;
RLessEqual. RelationalOperator ::= "<=" ;
RGreater. RelationalOperator ::= ">" ;
RGreaterEqual. RelationalOperator ::= ">=" ;
REqual. RelationalOperator ::= "==" ;
RNotEqual. RelationalOperator ::= "!=" ;


-- BOOLEAN OPERATORS -------------------------

BAnd. BooleanOperator ::= "and" ;
BOr. BooleanOperator ::= "or" ;
