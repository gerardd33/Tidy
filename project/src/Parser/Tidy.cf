-- GLOBAL -------------------------------------

token UpperCaseIdent (upper (letter | digit | '_')*) ;
token LowerCaseIdent (lower (letter | digit | '_')*) ;


-- PROGRAMS -----------------------------------

entrypoints Program ;
ProgramEntrypoint. Program ::= [ClassDecl] ;


-- COMMENTS -----------------------------------

comment "//" ;
comment "/*" "*/" ;


-- CLASS DECLARATIONS -------------------------

ClassIdentifier. ClassIdent ::= UpperCaseIdent ;

separator nonempty ClassDecl "" ;
ClassDeclaration. ClassDecl ::= AbstractModifier ClassTypeModifier "class" ClassType Inheritance ClassBody ;

SuperclassAbsent. Inheritance ::= ;
SuperclassPresent. Inheritance ::= "extends" ClassType ;

ClassBodyEmpty. ClassBody ::= ;
ClassBodyFilled. ClassBody ::= "{" ValuesSection VariablesSection FunctionsSection ActionsSection "}" ;


-- CLASS MODIFIERS ----------------------------

MMutable. ClassTypeModifier ::= "mutable" ;
MImmutable. ClassTypeModifier ::= "immutable" ;
MSingleton. ClassTypeModifier ::= "singleton" ;

MConcrete. AbstractModifier ::= ;
MAbstract. AbstractModifier ::= "abstract" ;


-- CLASS SECTIONS -----------------------------

ValuesAbsent. ValuesSection ::= ;
ValuesPresent. ValuesSection ::= "values:" "{" [ObjectDecl] "}" ;

VariablesAbsent. VariablesSection ::= ;
VariablesPresent. VariablesSection ::= "variables:" "{" [ObjectDecl] "}" ;

FunctionsAbsent. FunctionsSection ::= ;
FunctionsPresent. FunctionsSection ::= "functions:" "{" [FunctionDecl] "}" ;

ActionsAbsent. ActionsSection ::= ;
ActionsPresent. ActionsSection ::= "actions:" "{" [ActionDecl] "}" ;


-- OBJECTS ------------------------------------

ObjectIdentifier. ObjectIdent ::= LowerCaseIdent ;
ObjectTypeClass. ObjectType ::=  ClassType ;
ObjectTypeFunction. ObjectType ::= "get" MethodType ;
ObjectTypeAction. ObjectType ::= "do" MethodType ;

separator nonempty ClassType "," ;
GeneralClassType. ClassType ::= ClassIdent GenericParameter ;

GenericParameterAbsent. GenericParameter ::= ;
GenericParameterPresent. GenericParameter ::= "[" [ClassType] "]" ;

separator ObjectDecl "" ;
ObjectDeclaration. ObjectDecl ::= VisibilityModifier ObjectDeclProper ";" ;

MPublic. VisibilityModifier ::= ;
MPrivate. VisibilityModifier ::= "private" ;

separator ObjectDeclProper "," ;
ObjectDeclarationProper. ObjectDeclProper ::= ObjectIdent ":" ObjectType Initialization ;

Uninitialized. Initialization ::= ;
Initialized. Initialization ::= "=" Expr ;


-- METHODS ------------------------------------

MethodIdentifier. MethodIdent ::= LowerCaseIdent ;
MethodTypeSignature. MethodType ::= ParamList "->" ObjectType;
ParameterList. ParamList ::= "(" [ObjectDeclProper] ")" ;

separator FunctionDecl "" ;
FunctionDeclaration. FunctionDecl ::= OverrideModifier VisibilityModifier MethodIdent ":" MethodType "=" FunctionBody ;

MNonOverriding. OverrideModifier ::= ;
MOverride. OverrideModifier ::= "override" ;

FunctionBodyOneLine. FunctionBody ::= Expr ;
FunctionBodyMultiLine. FunctionBody ::= "{" Expr "}" WithValues ;

WithValuesAbsent. WithValues ::= ;
WithValuesPresent. WithValues ::= "with" ValuesSection ;

separator ActionDecl "" ;
ActionDeclaration. ActionDecl ::= OverrideModifier VisibilityModifier MethodIdent ":" MethodType "=" ActionBody ;

ActionBodyOneLine. ActionBody ::= Expr ;
ActionBodyMultiLine. ActionBody ::= "{" [Expr] "}" ;


-- EXPRESSIONS --------------------------------

separator Expr "" ;
coercions Expr 10 ;

ELiteral. Expr10 ::= Literal ;
ELocalValue. Expr10 ::= ObjectIdent ;

EGetExpression. Expr9 ::= GetExpr ;
EDoExpression. Expr9 ::= DoExpr ;

EConstructorCall. Expr8 ::= CtorCall ;

ELambdaFunction. Expr7 ::= LambdaFunction ;
ELambdaAction. Expr7 ::= LambdaAction ;

EImperativeControlFlow. Expr6 ::= ImperativeControlFlow ;
EFunctionalControlFlow. Expr6 ::= FunctionalControlFlow ;

ELocalDeclaration. Expr5 ::= LocalDecl ;

EUnaryNot. Expr4 ::= "not" Expr5 ;
EUnaryMinus. Expr4 ::= "-" Expr5 ;

EMultiply. Expr3 ::= Expr3 "*" Expr4 ;
EDivide. Expr3 ::= Expr3 "/" Expr4 ;
EModulo. Expr3 ::= Expr3 "%" Expr4 ;

EAdd. Expr2 ::= Expr2 "+" Expr3 ;
ESubtract. Expr2 ::= Expr2 "-" Expr3 ;
EConcatenate. Expr2 ::= Expr2 "++" Expr3 ;

ERelationalOperator. Expr1 ::= Expr1 RelationalOperator Expr2 ;

EBooleanOperator. Expr ::= Expr1 BooleanOperator Expr ;


-- LITERALS -----------------------------------

LInt. Literal ::= Integer ;
LBool. Literal ::= Boolean ;
LChar. Literal ::= Char ;
LString. Literal ::= String ;
LVoid. Literal ::= Void ;

BTrue. Boolean ::= "True" ;
BFalse. Boolean ::= "False" ;
VPass. Void ::= "Pass" ;


-- LOCAL DECLARATIONS -------------------------

LocalValueDeclaration. LocalDecl ::= "val" ObjectDecl ;
LocalVariableDeclaration. LocalDecl ::= "var" ObjectDecl ;


-- LAMBDAS ------------------------------------

LambdaFunctionOneLine. LambdaFunction ::= "get" ParamList "->" Expr ";" ;
LambdaFunctionMultiLine. LambdaFunction ::= "get" ParamList "->" "{" Expr "}" ;

LambdaActionOneLine. LambdaAction ::= "do" ParamList "->" Expr ";" ;
LambdaActionMultiLine. LambdaAction ::= "do" ParamList "->" "{" [Expr] "}" ;


-- FUNCTION AND CONSTRUCTOR CALLS -------------

ArgumentListAbsent. ArgList ::= ;
ArgumentListPresent. ArgList ::= "(" [MethodArg] ")" ;

separator MethodArg "," ;
MethodArgument. MethodArg ::= Expr ;

CallFunction. FunctionCall ::= "." MethodIdent ArgList ;
CallAction. ActionCall ::= "#" MethodIdent ArgList ;
CallConstructor. CtorCall ::= ClassType ArgList ;


-- GET EXPRESSIONS ----------------------------

GetExpressionInstance. GetExpr ::= ObjectIdent FunctionCall ;
GetExpressionStatic. GetExpr ::= ClassType FunctionCall ;
GetExpressionChain. GetExpr ::= GetExpr FunctionCall ;


-- DO EXPRESSIONS -----------------------------

DoExpressionInstance. DoExpr ::= ObjectIdent ActionCall ;
DoExpressionStatic. DoExpr ::= ClassType ActionCall ;
DoExpressionChain. DoExpr ::= GetExpr ActionCall ;


-- IMPERATIVE CONTROL FLOW --------------------

IWhile. ImperativeControlFlow ::= "while" "(" Expr ")" "{" [Expr] "}" ;
IForeach. ImperativeControlFlow ::= "for" "(" ObjectDecl "in" Expr ")" "{" [Expr] "}" ;
IIf. ImperativeControlFlow ::= "if" "(" Expr ")" "{" [Expr] "}" OptionalElseBranch ;

IElsePresent. OptionalElseBranch ::= "else" "{" [Expr] "}" ;
IElseIf. OptionalElseBranch ::= "elif" "(" Expr ")" "{" [Expr] "}" OptionalElseBranch ;
IElseAbsent. OptionalElseBranch ::= ;


-- FUNCTIONAL CONTROL FLOW --------------------

FIfThenElse. FunctionalControlFlow ::= "if" "(" Expr ")" ThenBranch ElseBranch ;

FThenOneLine. ThenBranch ::= "then" Expr ";" ;
FThenMultiLine. ThenBranch ::= "then" "{" Expr "}" ;
FElseOneLine. ElseBranch ::= "else" Expr ";" ;
FElseMultiLine. ElseBranch ::= "else" "{" Expr "}" ;
FElseIf. ElseBranch ::= "elif" "(" Expr ")" ThenBranch ElseBranch ;

FMatchCase. FunctionalControlFlow ::= "match" Expr "{" [MatchCase] "}" ;

separator MatchCase "" ;
FCase. MatchCase ::= "case" Pattern "->" Expr ;

FTypePattern. Pattern ::= ClassType ;


-- RELATIONAL OPERATORS -----------------------

RLess. RelationalOperator ::= "<" ;
RLessEqual. RelationalOperator ::= "<=" ;
RGreater. RelationalOperator ::= ">" ;
RGreaterEqual. RelationalOperator ::= ">=" ;
REqual. RelationalOperator ::= "==" ;
RNotEqual. RelationalOperator ::= "!=" ;


-- BOOLEAN OPERATORS --------------------------

BAnd. BooleanOperator ::= "and" ;
BOr. BooleanOperator ::= "or" ;
